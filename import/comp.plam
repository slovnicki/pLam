  ___________________________________________
  ||*         ___ ___  __  __ ___         *||
  ||         / __| _ \|  \/  | _ \         ||
  ||	       \___|___/|_\__/_|  _/         ||
  ||                         |_|           ||
  |\     -----------------------------     /|
  ===========================================
  |                                         |
  |   library for:                          |
  | defining partial recursive functions    |
  | [but preferably total ;)]               |
  |                                         |
  +-----------------------------------------+

--==========================================--
               BASIC FUNCTIONS
--==========================================--
T = \x y. x
F = \x y. y
P = \n f x. n (\g h. h (g f)) (\u. x) (\u. u)
isZ = \n. n (\x. F) T

--==========================================--
            FIXED POINT COMBINATOR 
--==========================================--
Y = \f. (\x. f(x x)) (\x. f(x x))

--==========================================--
              INITIAL FUNCTIONS 
--==========================================--
Z = \x. 0
S = \n f x. f (n f x)
I11 = \x. x 
I12 = \x y. x
I22 = \x y. y
I13 = \x y z. x
I23 = \x y z. y
I33 = \x y z. z

--==========================================--
                 COMPOSITION 
--==========================================--
C1 = \g h. \x. g (h x)
C2 = \g h. \x y. g (h x y) 
C3 = \g h. \x y z. g (h x y z) 

--==========================================--
             PRIMITIVE RECURSION 
--==========================================--
PR0 = \g h. Y (\f k. (isZ k) g (h (P k) (f (P k))))
PR1 = \g h. Y (\f x y. (isZ y) (g x) (h x (P y) (f x (P y))))

--==========================================--
                MINIMIZATION 
--==========================================--
MIN1x = \g. Y (\h x. (isZ (g x)) x (h (S x)))
MIN1 = \g. (Y (\h x. (isZ (g x)) x (h (S x)))) 0
MIN2x = \g. Y (\h x y. (isZ (g x y)) y (h x (S y)))
MIN2 = \g. (Y (\h x y. (isZ (g x y)) y (h x (S y)))) 0


